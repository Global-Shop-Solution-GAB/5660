Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
'Author: Arturo Almaguer
'Date: 17 August 2021
'Customer: Midland Metal Products, charged as VT

F.Intrinsic.Control.Try

	Function.Intrinsic.UI.UsePixels ' Allows you to use Pixels instead of Twips throughout
	
	V.Local.sError.Declare(String)
	V.Local.sTransactionID.Declare(String)
	
	V.Local.sTransactionID.Set(V.Passed.DATA-TRANSID)
	
	F.Intrinsic.Control.CallSub(BinSearch,"TransactionID", V.Local.sTransactionID)
			
F.Intrinsic.Control.Catch

	F.Intrinsic.String.Build("<font color={6}red{6}><b>Project: {0}{1}<br>{1}<br>Subroutine: {2}{1}<br>Error Occurred {3} with description {4}{1}<br>{1}<br>GAB Version: {5}</b></font>",V.Caller.ScriptFile,V.Ambient.Newline,V.Ambient.SubroutineCalledFrom,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Ambient.DblQuote,V.Local.sError)
	F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
	F.Intrinsic.Control.End
	
F.Intrinsic.Control.EndTry

Program.Sub.Main.End
Program.Sub.BinSearch.Start
'This subroutine triggers Bin Search custom mobile transaction

F.Intrinsic.Control.Try

	V.Local.sSQL.Declare(String)
	V.Local.sSQL2.Declare(String)
	V.Local.sTransactionID.Declare(String)
	V.Local.sError.Declare(String)
	V.Local.sReturn.Declare(String)
	V.Local.sJob.Declare(String)
	V.Local.sSuffix.Declare(String)
	V.Local.sSequence.Declare(String)
	V.Local.sPart.Declare(String)
	
	V.Local.sTransactionID.Set(V.Args.TransactionID)
	
	'Getting current transaction data
	F.Intrinsic.String.Build("select RTRIM(FIELD_1) PART, RTRIM(FIELD_2) JSS from V_WIRELESS_LINE where TRANS_ID = '{0}';", V.Local.sTransactionID, V.Local.sSQL)
	F.ODBC.Connection!Con.OpenCompanyConnection
	F.Data.DataTable.CreateFromSQL("dtTransaction", "Con", V.Local.sSQL, True)
	F.ODBC.Connection!Con.Close

	'Setting variables
	F.Intrinsic.Control.If(V.DataTable.dtTransaction(0).PART!FieldValTrim,<>,"")
		V.Local.sPart.Set(V.DataTable.dtTransaction(0).PART!FieldValTrim)
	F.Intrinsic.Control.EndIf
	
	F.Intrinsic.Control.If(V.DataTable.dtTransaction(0).JSS!FieldValTrim,<>,"")
		F.Intrinsic.String.Left(V.DataTable.dtTransaction(0).JSS!FieldValTrim,6,V.Local.sJob)
		F.Intrinsic.String.Mid(V.DataTable.dtTransaction(0).JSS!FieldValTrim, 7, 3, V.Local.sSuffix)
		F.Intrinsic.String.Right(V.DataTable.dtTransaction(0).JSS!FieldValTrim,6,V.Local.sSequence)
	F.Intrinsic.Control.EndIf
	
	'Displaying error message if part AND job are submitted with no value in field YES
	F.Intrinsic.Control.If(V.Local.sPart,=,"", "AND", V.DataTable.dtTransaction(0).JSS!FieldValTrim,=,"")
		F.Intrinsic.String.Build("<font color={0}red{0}><b>Part and Job cannot be blank</b></font>",V.Ambient.DblQuote,V.Local.sError)
		F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
		F.Intrinsic.Control.End
	F.Intrinsic.Control.EndIf
	
	'Opening connection
	F.ODBC.Connection!Con.OpenCompanyConnection
	
	F.Intrinsic.Control.If(V.Local.sPart,<>,"", "AND", V.DataTable.dtTransaction(0).JSS!FieldValTrim,<>,"")
	
		'Checking if part for that job exists in custom table
		F.Intrinsic.String.Build("select RTRIM(BIN) BIN, RTRIM(PART) PART, RTRIM(JOB) + '-' + RTRIM(SUFFIX) + '-' + RTRIM(SEQUENCE) as 'JOB-SUF-SEQ', QTY from GCG_5660_BIN_PARTS where PART = '{0}' and JSS = '{1}' order by DATE_IN;", V.Local.sPart, V.DataTable.dtTransaction(0).JSS!FieldValTrim, V.Local.sSQL)
		F.ODBC.Connection!Con.ExecuteAndReturn(V.Local.sSQL,V.Local.sSQL2)
		F.Intrinsic.Control.If(V.Local.sSQL2,=,"")
			
			'Displaying error message
			F.Intrinsic.String.Build("<font color={0}red{0}><b>WARNING: No entry for Part {1}{2}<br> Linked to Job {3}-{4}-{5}{2}<br></b></font>{2}<br>",V.Ambient.DblQuote,V.Local.sPart,V.Ambient.NewLine,V.Local.sJob, V.Local.sSuffix, V.Local.sSequence, V.Local.sError)
			F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
			F.ODBC.Connection!Con.Close
			F.Intrinsic.Control.End
			
		F.Intrinsic.Control.Else
			
			'Creating dt to get all bins a part is stored in
			F.Data.DataTable.CreateFromSQL("dtPart", "Con", V.Local.sSQL, True)
		
		F.Intrinsic.Control.EndIf
	
	F.Intrinsic.Control.ElseIf(V.Local.sPart,<>,"", "AND", V.DataTable.dtTransaction(0).JSS!FieldValTrim,=,"")
	
		'Checking if part exists in custom table
		F.Intrinsic.String.Build("select RTRIM(BIN) BIN, RTRIM(PART) PART, RTRIM(JOB) + '-' + RTRIM(SUFFIX) + '-' + RTRIM(SEQUENCE) as 'JOB-SUF-SEQ', QTY from GCG_5660_BIN_PARTS where PART = '{0}' order by DATE_IN;", V.Local.sPart, V.Local.sSQL)
		F.ODBC.Connection!Con.ExecuteAndReturn(V.Local.sSQL,V.Local.sSQL2)
		F.Intrinsic.Control.If(V.Local.sSQL2,=,"")
			
			'Displaying error message
			F.Intrinsic.String.Build("<font color={0}red{0}><b>WARNING: No entry for Part {1} in custom table</b></font>{2}<br>",V.Ambient.DblQuote,V.Local.sPart,V.Ambient.NewLine, V.Local.sError)
			F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
			F.ODBC.Connection!Con.Close
			F.Intrinsic.Control.End
			
		F.Intrinsic.Control.Else
			
			'Creating dt to get all bins a part is stored in
			F.Data.DataTable.CreateFromSQL("dtPart", "Con", V.Local.sSQL, True)
		
		F.Intrinsic.Control.EndIf
	
	F.Intrinsic.Control.Else
	
		'Checking if job/suf/seq exists in custom table
		F.Intrinsic.String.Build("select RTRIM(BIN) BIN, RTRIM(PART) PART, RTRIM(JOB) + '-' + RTRIM(SUFFIX) + '-' + RTRIM(SEQUENCE) as 'JOB-SUF-SEQ', QTY from GCG_5660_BIN_PARTS where JSS = '{0}' order by DATE_IN;", V.DataTable.dtTransaction(0).JSS!FieldValTrim, V.Local.sSQL)
		F.ODBC.Connection!Con.ExecuteAndReturn(V.Local.sSQL,V.Local.sSQL2)
		F.Intrinsic.Control.If(V.Local.sSQL2,=,"")
			
			'Displaying error message
			F.Intrinsic.String.Build("<font color={0}red{0}><b>WARNING: No entry for Job {1}-{3}-{4} in custom table</b></font>{2}<br>",V.Ambient.DblQuote,V.Local.sJob,V.Ambient.NewLine, V.Local.sSuffix, V.Local.sSequence, V.Local.sError)
			F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
			F.ODBC.Connection!Con.Close
			F.Intrinsic.Control.End
			
		F.Intrinsic.Control.Else
			
			'Creating dt to get all bins a JOB/SUF/SEQ is stored in
			F.Data.DataTable.CreateFromSQL("dtPart", "Con", V.Local.sSQL, True)
		
		F.Intrinsic.Control.EndIf

	F.Intrinsic.Control.EndIf
	
	'Closing connection
	F.ODBC.Connection!Con.Close
	
	'Exporting dt to HTML so it can be post back to the user in GS Mobile screen
	F.Data.DataTable.ExportHTML("dtPart","", "***USETABLE***", "BinParts", True, V.Local.sReturn)
	F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sReturn)
	
F.Intrinsic.Control.Catch

	F.Intrinsic.String.Build("<font color={6}red{6}><b>Project: {0}{1}<br>{1}<br>Subroutine: {2}{1}<br>Error Occurred {3} with description {4}{1}<br>{1}<br>GAB Version: {5}</b></font>",V.Caller.ScriptFile,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Ambient.DblQuote,V.Local.sError)
	F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
	F.Intrinsic.Control.End
	
F.Intrinsic.Control.EndTry

Program.Sub.BinSearch.End

Program.Sub.Comments.Start
${$5$}$3.0.0.0$}$1
${$6$}$apadilla$}$20210819133609544$}$Lag9STqBJOmNXSbd5+msRcBWPGz0aFqSGjpgCwFu86BYHeJgtg46L3yQSJ1Yiq0bgcYRoou01/cRI2eXoFB5iw==
Program.Sub.Comments.End