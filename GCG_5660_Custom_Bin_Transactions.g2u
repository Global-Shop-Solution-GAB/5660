Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
'Author: Arturo Almaguer
'Date: 20 July 2021
'Customer: Midland Metal Products, quote number 12708
'This program is linked to two different custom mobile transactions:
'WIP to BIN: allows the user to scan JOB, SUFFIX, SEQ in to a single field
'BIN Audit: allows the user to scan a bin and all parts that exists in that bin will be displayed in a table format in GS Mobile screen

F.Intrinsic.Control.Try

	Function.Intrinsic.UI.UsePixels ' Allows you to use Pixels instead of Twips throughout
	
	V.Local.sError.Declare(String)
	V.Local.sTransactionID.Declare(String)
	
	V.Local.sTransactionID.Set(V.Passed.DATA-TRANSID)
	
	F.Intrinsic.Control.CallSub(CheckTable)

	F.Intrinsic.Control.SelectCase(V.Caller.Hook)
		
		F.Intrinsic.Control.Case(39004)
		'39004 GS Mobile Custom 4 hook, for a custom mobile transcation ID 4
			F.Intrinsic.Control.CallSub(WIPTOBIN, "TransactionID", V.Local.sTransactionID)
		
		F.Intrinsic.Control.Case(39005)
		'39005 GS Mobile Custom 5 hook, for a custom mobile transcation ID 5
			F.Intrinsic.Control.CallSub(BinAudit,"TransactionID", V.Local.sTransactionID)
			
	F.Intrinsic.Control.EndSelect

F.Intrinsic.Control.Catch

	F.Intrinsic.String.Build("<font color={6}red{6}><b>Project: {0}{1}<br>{1}<br>Subroutine: {2}{1}<br>Error Occurred {3} with description {4}{1}<br>{1}<br>GAB Version: {5}</b></font>",V.Caller.ScriptFile,V.Ambient.Newline,V.Ambient.SubroutineCalledFrom,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Ambient.DblQuote,V.Local.sError)
	F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
	F.Intrinsic.Control.End
	
F.Intrinsic.Control.EndTry

Program.Sub.Main.End
Program.Sub.WIPTOBIN.Start
'This subroutine triggers WIP to Bin custom mobile transaction

F.Intrinsic.Control.Try

	V.Local.sSQL.Declare(String)
	V.Local.sJob.Declare(String)
	V.Local.sSuffix.Declare(String)
	V.Local.sSequence.Declare(String)
	V.Local.sPart.Declare(String)
	V.Local.sDescription.Declare(String)
	V.Local.sLMO.Declare(String)
	V.Local.sQty.Declare(String)
	V.Local.sBin.Declare(String)
	V.Local.sError.Declare(String)
	V.Local.sTransactionID.Declare(String)
	V.Local.sReturn.Declare(String)
	
	V.Local.sTransactionID.Set(V.Args.TransactionID)
	
	'Getting current transaction data
	F.Intrinsic.String.Build("select RTRIM(A.FIELD_1) JSS, RTRIM(A.FIELD_2) QUANTITY, RTRIM(A.FIELD_3) TO_BIN, RTRIM(B.ATTEMPTED_DATE) DATE_IN, RTRIM(B.ATTEMPTED_TIME) TIME_IN, RTRIM(B.USER_ID) USER_ID from V_WIRELESS_LINE A LEFT JOIN V_WIRELESS_HDR B ON A.TRANS_ID = B.TRANS_ID where A.TRANS_ID = '{0}';", V.Local.sTransactionID, V.Local.sSQL)
	F.ODBC.Connection!Con.OpenCompanyConnection
	F.Data.DataTable.CreateFromSQL("dtTransaction", "Con", V.Local.sSQL, True)
	F.ODBC.Connection!Con.Close
	
	'Setting variables
	V.Local.sQty.Set(V.DataTable.dtTransaction(0).QUANTITY!FieldValTrim)
	V.Local.sBin.Set(V.DataTable.dtTransaction(0).TO_BIN!FieldValTrim)
	F.Intrinsic.String.Left(V.DataTable.dtTransaction(0).JSS!FieldValTrim,6,V.Local.sJob)
	F.Intrinsic.String.Mid(V.DataTable.dtTransaction(0).JSS!FieldValTrim, 7, 3, V.Local.sSuffix)
	F.Intrinsic.String.Right(V.DataTable.dtTransaction(0).JSS!FieldValTrim,6,V.Local.sSequence)
	
	'Displaying error message if bin or qty are submitted with no value in fields YES
	F.Intrinsic.Control.If(V.Local.sQty,=,"","OR",V.Local.sBin,=,"")
		F.Intrinsic.String.Build("<font color={0}red{0}><b>Bin and Qty cannot be blank</b></font>",V.Ambient.DblQuote,V.Local.sError)
		F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
		F.Intrinsic.Control.End
	F.Intrinsic.Control.EndIf
	
	'Displaying error message if qty has a value of 0 YES
	F.Intrinsic.Control.If(V.Local.sQty,=,0)
		F.Intrinsic.String.Build("<font color={0}red{0}><b>Qty cannot be zero</b></font>",V.Ambient.DblQuote,V.Local.sError)
		F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
		F.Intrinsic.Control.End
	F.Intrinsic.Control.EndIf

	'Checking if job exists in GSS
	F.Intrinsic.String.Build("select RTRIM(PART) PART, RTRIM(DESCRIPTION) DESCRIPTION, RTRIM(LMO) LMO from V_JOB_OPERATIONS where JOB = '{0}' and SUFFIX = '{1}' and SEQ = '{2}';", V.Local.sJob, V.Local.sSuffix, V.Local.sSequence, V.Local.sSQL)
	F.ODBC.Connection!Con.OpenCompanyConnection
	F.ODBC.Connection!Con.ExecuteAndReturn(V.Local.sSQL,V.Local.sSQL)
	
	'If it is not blank it means that job exists, if is blank it means that job doesn't exist
	F.Intrinsic.Control.If(V.Local.sSQL, <>, "")
	
		'Splitting array to set new variables
		F.Intrinsic.String.Split(V.Local.sSQL, "*!*", V.Local.sSQL)
		V.Local.sPart.Set(V.Local.sSQL(0))
		V.Local.sDescription.Set(V.Local.sSQL(1))
		V.Local.sLMO.Set(V.Local.sSQL(2))
		F.Intrinsic.String.Format(V.Local.sQty,"#,###,##0.0000", V.Local.sQty)
		
		'If it is not equal to L it means that part and description are being pulled from V_JOB_OPERATIONS, if it equals to L it means that part and description are being pulled from V_JOB_HEADER
		F.Intrinsic.Control.If(V.Local.sLMO, <>, "L")
		
			'Checking if part, bin, job, suffix and seq exist already in custom table
			F.Intrinsic.String.Build("select * from GCG_5660_BIN_PARTS where PART = '{0}' and BIN = '{1}' and JOB = '{2}' and SUFFIX = '{3}' and SEQUENCE = '{4}';", V.Local.sPart, V.Local.sBin, V.Local.sJob, V.Local.sSuffix, V.Local.sSequence, V.Local.sSQL)
			F.ODBC.Connection!Con.ExecuteAndReturn(V.Local.sSQL,V.Local.sSQL)
			
			'If it is not blank just update data, if it is blank insert new record
			F.Intrinsic.Control.If(V.Local.sSQL, <>, "")
			
				F.Intrinsic.String.Build("update GCG_5660_BIN_PARTS set QTY = '{0}', MOBILE_USER = '{1}', DATE_IN = '{2}', TIME_IN = '{3}' where PART = '{4}' and BIN = '{5}' and JOB = '{6}' and SUFFIX = '{7}' and SEQUENCE = '{8}';",  V.Local.sQty, V.DataTable.dtTransaction(0).USER_ID!FieldValTrim, V.DataTable.dtTransaction(0).DATE_IN!FieldValTrim, V.DataTable.dtTransaction(0).TIME_IN!FieldValTrim, V.Local.sPart, V.Local.sBin, V.Local.sJob, V.Local.sSuffix, V.Local.sSequence, V.Local.sSQL)
				
			F.Intrinsic.Control.Else
			
				F.Intrinsic.String.Build("insert into GCG_5660_BIN_PARTS(PART, DESCRIPTION, BIN, QTY, JSS, JOB, SUFFIX, SEQUENCE, MOBILE_USER, DATE_IN, TIME_IN) values('{0}','{1}','{2}','{3}', '{4}{5}{6}','{4}','{5}','{6}','{7}','{8}','{9}');", V.Local.sPart, V.Local.sDescription, V.Local.sBin, V.Local.sQty, V.Local.sJob, V.Local.sSuffix, V.Local.sSequence, V.DataTable.dtTransaction(0).USER_ID!FieldValTrim, V.DataTable.dtTransaction(0).DATE_IN!FieldValTrim, V.DataTable.dtTransaction(0).TIME_IN!FieldValTrim, V.Local.sSQL)
				
			F.Intrinsic.Control.EndIf
			
		F.Intrinsic.Control.Else
		
			'Retrieving data from V_JOB_HEADER because it is a labor sequence
			F.Intrinsic.String.Build("select RTRIM(PART) PART, RTRIM(DESCRIPTION) DESCRIPTION from V_JOB_HEADER where JOB = '{0}' and SUFFIX = '{1}';", V.Local.sJob, V.Local.sSuffix, V.Local.sSQL)
			F.ODBC.Connection!Con.ExecuteAndReturn(V.Local.sSQL,V.Local.sSQL)
			'Splitting array to set new variables
			F.Intrinsic.String.Split(V.Local.sSQL, "*!*", V.Local.sSQL)
			V.Local.sPart.Set(V.Local.sSQL(0))
			V.Local.sDescription.Set(V.Local.sSQL(1))
			
			'Checking if part, bin, job, suffix and seq exist already in custom table
			F.Intrinsic.String.Build("select * from GCG_5660_BIN_PARTS where PART = '{0}' and BIN = '{1}' and JOB = '{2}' and SUFFIX = '{3}' and SEQUENCE = '{4}';", V.Local.sPart, V.Local.sBin, V.Local.sJob, V.Local.sSuffix, V.Local.sSequence, V.Local.sSQL)
			F.ODBC.Connection!Con.ExecuteAndReturn(V.Local.sSQL,V.Local.sSQL)
			
			'If it is not blank just update data, if it is blank insert new record
			F.Intrinsic.Control.If(V.Local.sSQL, <>, "")
			
				F.Intrinsic.String.Build("update GCG_5660_BIN_PARTS set QTY = '{0}', MOBILE_USER = '{1}', DATE_IN = '{2}', TIME_IN = '{3}' where PART = '{4}' and BIN = '{5}' and JOB = '{6}' and SUFFIX = '{7}' and SEQUENCE = '{8}';",  V.Local.sQty, V.DataTable.dtTransaction(0).USER_ID!FieldValTrim, V.DataTable.dtTransaction(0).DATE_IN!FieldValTrim, V.DataTable.dtTransaction(0).TIME_IN!FieldValTrim, V.Local.sPart, V.Local.sBin, V.Local.sJob, V.Local.sSuffix, V.Local.sSequence, V.Local.sSQL)
	
			F.Intrinsic.Control.Else
			
				F.Intrinsic.String.Build("insert into GCG_5660_BIN_PARTS(PART, DESCRIPTION, BIN, QTY, JSS, JOB, SUFFIX, SEQUENCE, MOBILE_USER, DATE_IN, TIME_IN) values('{0}','{1}','{2}','{3}', '{4}{5}{6}','{4}','{5}','{6}','{7}','{8}','{9}');", V.Local.sPart, V.Local.sDescription, V.Local.sBin, V.Local.sQty, V.Local.sJob, V.Local.sSuffix, V.Local.sSequence, V.DataTable.dtTransaction(0).USER_ID!FieldValTrim, V.DataTable.dtTransaction(0).DATE_IN!FieldValTrim, V.DataTable.dtTransaction(0).TIME_IN!FieldValTrim, V.Local.sSQL)
				
			F.Intrinsic.Control.EndIf
			
		F.Intrinsic.Control.EndIf
		
		'Executing SQL query
		F.ODBC.Connection!Con.Execute(V.Local.sSQL)
		
		'testing
		F.Intrinsic.String.Build("<font color={0}green{0}><b>Part {1} linked to Job {5}-{6}-{7} has been moved into Bin {3} with Quantity {4}</b></font>{2}<br>",V.Ambient.DblQuote, V.Local.sPart, V.Ambient.NewLine, V.Local.sBin, V.Local.sQty, V.Local.sJob, V.Local.sSuffix, V.Local.sSequence, V.Local.sReturn)
		F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sReturn)
	
	F.Intrinsic.Control.Else
		'YES
		F.Intrinsic.String.Build("<font color={0}red{0}><b>WARNING: No entry in Global Operations for Job {1}-{3}-{4}, Job doesn't exist</b></font>{2}<br>",V.Ambient.DblQuote,V.Local.sJob,V.Ambient.NewLine, V.Local.sSuffix, V.Local.sSequence, V.Local.sError)
		F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
		
	F.Intrinsic.Control.EndIf
	F.ODBC.Connection!Con.Close

F.Intrinsic.Control.Catch

	F.Intrinsic.String.Build("<font color={6}red{6}><b>Project: {0}{1}<br>{1}<br>Subroutine: {2}{1}<br>Error Occurred {3} with description {4}{1}<br>{1}<br>GAB Version: {5}</b></font>",V.Caller.ScriptFile,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Ambient.DblQuote,V.Local.sError)
	F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
	F.Intrinsic.Control.End
	
F.Intrinsic.Control.EndTry

Program.Sub.WIPTOBIN.End
Program.Sub.CheckTable.Start

V.Local.bExists.Declare(Boolean)

F.ODBC.Connection!Con.OpenCompanyConnection
F.ODBC.Connection!Con.TableExists("GCG_5660_BIN_PARTS", V.Local.bExists)
	F.Intrinsic.Control.If(V.Local.bExists, =, False)
		F.ODBC.Connection!Con.Execute("CREATE TABLE GCG_5660_BIN_PARTS (PART CHAR(20) NOT NULL, DESCRIPTION CHAR(30), BIN CHAR(10) NOT NULL, QTY NUMERIC(12,4) NOT NULL, JSS CHAR(15) ,JOB CHAR(6), SUFFIX CHAR(3), SEQUENCE CHAR(6), MOBILE_USER CHAR(30), DATE_IN DATE, TIME_IN TIME);")
	F.Intrinsic.Control.EndIf
F.ODBC.Connection!Con.Close

Program.Sub.CheckTable.End
Program.Sub.BinAudit.Start
'This subroutine triggers Bin Audit custom mobile transaction

F.Intrinsic.Control.Try

	V.Local.sSQL.Declare(String)
	V.Local.sBin.Declare(String)
	V.Local.sTransactionID.Declare(String)
	V.Local.sError.Declare(String)
	V.Local.sReturn.Declare(String)
	
	V.Local.sTransactionID.Set(V.Args.TransactionID)
	
	'Getting current transaction data
	F.Intrinsic.String.Build("select RTRIM(FIELD_1) BIN from V_WIRELESS_LINE where TRANS_ID = '{0}';", V.Local.sTransactionID, V.Local.sSQL)
	F.ODBC.Connection!Con.OpenCompanyConnection
	F.ODBC.Connection!Con.ExecuteAndReturn(V.Local.sSQL,V.Local.sBin)
	
	'Displaying error message if transid doesn't appear in table and ending program
	F.Intrinsic.Control.If(V.Ambient.ExecuteAndReturnEOF)
		F.Intrinsic.String.Build("<font color={0}red{0}><b>Could not read the value selected</b></font>",V.Ambient.DblQuote,V.Local.sError)
		F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
		F.ODBC.Connection!Con.Close
		F.Intrinsic.Control.End
	F.Intrinsic.Control.EndIf
	
	'Displaying error message if bin is submitted with no value in field
	F.Intrinsic.Control.If(V.Local.sBin,=,"")
		F.Intrinsic.String.Build("<font color={0}red{0}><b>Bin cannot be blank</b></font>",V.Ambient.DblQuote,V.Local.sError)
		F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
		F.ODBC.Connection!Con.Close
		F.Intrinsic.Control.End
	F.Intrinsic.Control.EndIf
	
	'Displaying error message if bin doesn't exist
	F.Intrinsic.String.Build("select RTRIM(BIN_DESCRIPTION) from V_BIN_MASTER where BIN = '{0}';",V.Local.sBin,V.Local.sSQL)
	F.ODBC.Connection!Con.ExecuteAndReturn(V.Local.sSQL,V.Local.sSQL)
	F.Intrinsic.Control.If(V.Local.sSQL,=,"")
		F.Intrinsic.String.Build("<font color={0}red{0}><b>WARNING: No entry in Global Bin Master for Bin {1}, Bin doesn't exist</b></font>{2}<br>",V.Ambient.DblQuote,V.Local.sBin,V.Ambient.NewLine,V.Local.sError)
		F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
		F.ODBC.Connection!Con.Close
		F.Intrinsic.Control.End
	F.Intrinsic.Control.EndIf
	
	'Creating dt to get all parts stored in bin
	F.Intrinsic.String.Build("select RTRIM(PART) PART, RTRIM(DESCRIPTION) DESCRIPTION, RTRIM(JOB) JOB, RTRIM(SUFFIX) SUFFIX, RTRIM(SEQUENCE) SEQUENCE, QTY, DATE_IN, TIME_IN from GCG_5660_BIN_PARTS where BIN = '{0}' order by DATE_IN;", V.Local.sBin, V.Local.sSQL)
	F.Data.DataTable.CreateFromSQL("dtBin", "Con", V.Local.sSQL, True)
	F.ODBC.Connection!Con.Close
	
	'Exporting dt to HTML so it can be post back to the user in GS Mobile screen
	F.Data.DataTable.ExportHTML("dtBin","", "***USETABLE***", "BinParts", True, V.Local.sReturn)
	F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sReturn)
	
F.Intrinsic.Control.Catch

	F.Intrinsic.String.Build("<font color={6}red{6}><b>Project: {0}{1}<br>{1}<br>Subroutine: {2}{1}<br>Error Occurred {3} with description {4}{1}<br>{1}<br>GAB Version: {5}</b></font>",V.Caller.ScriptFile,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Ambient.DblQuote,V.Local.sError)
	F.Global.Mobile.SetCustomResult(V.Caller.CompanyCode,V.Local.sTransactionID,V.Local.sError)
	F.Intrinsic.Control.End
	
F.Intrinsic.Control.EndTry

Program.Sub.BinAudit.End

Program.Sub.Comments.Start
${$5$}$3.0.0.0$}$1
${$6$}$apadilla$}$20210728162210169$}$Lag9STqBJOmNXSbd5+msRcBWPGz0aFqSGjpgCwFu86Auhe7igYJdAJqciH6jmIPS76Y1HVEb6C0WPh8Qc/jfvg==
Program.Sub.Comments.End